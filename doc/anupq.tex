%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%A  anupq.tex    ANUPQ documentation - non-interactive f'ns    Eamonn O'Brien
%A                                                             & Frank Celler
%A                                                           & Benedikt Rothe
%%
%A  @(#)$Id$
%%
%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Chapter{Non-interactive ANUPQ functions}

Here we describe all the non-interactive functions of the {\ANUPQ}  share
package; i.e.~``one-shot'' functions that invoke the `pq' binary in  such
a way that once {\GAP} has got what it needs, the `pq' binary is  allowed
to exit. It is expected that most of the time users will only need  these
functions. The functions interface with three of the four algorithms (see
Chapter~"Introduction") provided by the  ANU  `pq'  C  program,  and  are
mainly grouped according to the algorithm of the `pq' binary they  relate
to.

In Section~"Computing p-Quotients", we describe the functions  that  give
access to the $p$-quotient algorithm.

Section~"Computing Standard Presentations" describe functions  that  give
access to the standard presentation algorithm.

Section~"Testing  p-Groups  upon  Isomorphism"  describe  functions  that
implement  an  isomorphism  test  for  $p$-groups  using   the   standard
presentation algorithm.

In Section~"Computing Descendants of a p-Group",  we  describe  functions
that give access to the $p$-group generation algorithm.

To use any of the functions one must have at some stage previously typed:

\beginexample
gap> RequirePackage("anupq");
\endexample

(the response of which we have  omitted;  see~"Loading  the  ANUPQ  Share
Package"). We will nevertheless remind you of this for each function,  in
case you happen to be reading about that particular function via  on-line
help.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing  p-Quotients}

\>Pq( <F> : <options> ) F

returns for the finitely presented group <F>,  the  $p$-quotient  of  <F>
specified by <options>, as a pc-group. Behind the colon, <options>  is  a
selection of the  options  from  the  following  list  should  be  given,
separated by commas like record components (see~"ref:function  call  with
options"). As a minimum the user must supply values for the  `Prime'  and
`ClassBound' options. `Pq' may also be called with no  arguments  or  one
integer  argument,  in  which  case  it  is  being   used   interactively
(see~"Pq!interactive");  the  same  options  may  be  used,  except  that
`SetupFile'  and  `PqWorkspace'  are  ignored  by  the  interactive  `Pq'
function. Section~"Hints and Warnings regarding the use of Options" gives
some important hints and warnings regarding option usage; if you  haven't
read this section, please take the time to read it now.

\beginitems

`Prime := <p>' & 
Specifies that the $p$-quotient for the prime <p> should be computed.

`ClassBound := <n>' & 
Specifies that the $p$-quotient computed has lower exponent-$p$ class  at
most <n>.

`Exponent := <n>' & 
Specifies that the $p$-quotient computed has exponent <n>. By default, no
exponent law is enforced.

`Metabelian' & 
Specifies that the largest metabelian  $p$-quotient  be  constructed.  By
default this restriction is not enforced.

`OutputLevel := <n>' &
Specifies the level of ``verbosity'' of the information output by the ANU
`pq' binary when computing a pc presentation; <n> must be an  integer  in
the range 0 to 3. `OutputLevel := 0' displays no output, `OutputLevel  :=
1' displays minimal output (which  is  the  `pq'  binary's  default)  and
`OutputLevel's for <n> greater than `1' give progressively  more  output.
To see these messages from the `pq' binary, the `InfoANUPQ' level must be
set to 2 (see~"InfoANUPQ").

`SetupFile := <filename>' &
Non-interactively, this option directs that `pq' should not be called and
that an input file  with  name  <filename>  (a  string),  containing  the
commands necessary for the  ANU  `pq'  standalone,  be  constructed.  The
commands written to <filename> are also `Info'-ed behind  a  ```ToPQ> '''
prompt at `InfoANUPQ' level  3  (see~"InfoANUPQ").  In  this  case,  `Pq'
returns `true'. Interactively, `SetupFile' is ignored.

`PqWorkspace := <workspace>' &
Non-interactively, this option sets the memory used by the  `pq'  binary.
It sets the maximum number of integer-sized elements to allocate  in  its
main storage array. By default, the  `pq'  binary  sets  this  figure  to
10000000. Interactively, `PqWorkspace' is ignored;  the  memory  used  in
this  case  may  be  set  by   giving   `PqStart'   a   second   argument
(see~"PqStart").

\enditems

(`Pq' requires the {\ANUPQ} share package; see~"Loading  the  ANUPQ  Share
Package".)

See also "PqEpimorphism".

We now give a few examples of the use of `Pq'.

\beginexample
gap> RequirePackage("anupq");; # does nothing if ANUPQ is already loaded
gap> # First we get a p-quotient of a free group of rank 2
gap> F := FreeGroup("a", "b");; a := F.1;; b := F.2;;
gap> Pq( F : Prime := 2, ClassBound := 3 ); 
<pc group of size 1024 with 10 generators>
gap> # Now let us get a p-quotient of a finitely generated group
gap> G := F / [a^4, b^4];
<fp group on the generators [ a, b ]>
gap> Pq( G : Prime := 2, ClassBound := 3 ); 
<pc group of size 256 with 8 generators>
gap> # Now let's get a different p-quotient of the same group
gap> Pq( G : Prime := 2, ClassBound := 3, Exponent := 4); 
<pc group of size 128 with 7 generators>
gap> # Now we'll get a p-quotient of another finitely generated group 
gap> H := F / [ a^25, Comm(Comm(b, a), a), b^5 ];;
gap> Pq( H : Prime := 5, ClassBound := 5, Metabelian);
<pc group of size 78125 with 7 generators>
\endexample

The non-interactive `Pq' function also allows the options to be passed in
two other ways; these alternatives have been included for those  familiar
with the {\GAP}~3 version of the {\ANUPQ} package; the  preferred  method
of passing options is the one already described.  Firstly,  they  may  be
passed in a record as a second argument; note that  any  boolean  options
must be set explicitly e.g.

\beginexample
gap> Pq( H, rec( Prime := 5, ClassBound := 5, Metabelian := true ) );
<pc group of size 78125 with 7 generators>
\endexample

It is also possible to pass them as extra arguments,  where  each  option
name appears as a string followed immediately by  its  value  (if  not  a
boolean option) e.g.

\beginexample
gap> Pq( H, "Prime", 5, "ClassBound", 5, "Metabelian" );             
<pc group of size 78125 with 7 generators>
\endexample

This method of passing options permits abbreviation; the only restriction
is that the abbreviation must be  unique.  So  `"Pr"'  may  be  used  for
`"Prime"', `"Class"' or even just `"C"' for `"ClassBound"', etc.

\>PqEpimorphism( <F> : <options> ) F

returns for the finitely presented group <F> an epimorphism from <F> onto
the $p$-quotient of <F> specified by <options>;  the  possible  <options>
and required <options> (`"Prime"' and `"ClassBound"')  are  as  for  `Pq'
(see~"Pq"). `PqEpimorphism' only differs from `Pq' in  what  it  outputs;
everything about what must/may be passed as input to  `PqEpimorphism'  is
the same  as  for  `Pq',  i.e.~`PqEpimorphism'  may  be  called  with  no
arguments or one integer  argument,  in  which  case  it  is  being  used
interactively   (see~"PqEpimorphism!interactive"),   and   the    options
`SetupFile'  and   `PqWorkspace'   are   ignored   by   the   interactive
`PqEpimorphism' function; and the same  alternative  methods  of  passing
options to  the  non-interactive  `Pq'  function  are  available  to  the
non-interactive version of `PqEpimorphism'.

\beginexample
gap> F := FreeGroup (2, "F");
<free group on the generators [ F1, F2 ]>
gap> phi := PqEpimorphism( F : Prime := 5, ClassBound := 2 );
[ F1, F2 ] -> [ f1, f2 ]
gap> Image( phi );
<pc group of size 3125 with 5 generators>
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing Standard Presentations}

\index{automorphisms!of $p$-groups}
\>PqStandardPresentation( <F>, <p> : <options> ) F
\>PqStandardPresentation( <F>, <G> : <options> ) F
\>StandardPresentation( <F>, <p> : <options> ) M
\>StandardPresentation( <F>, <G> : <options> ) M

return, for the finitely presented group <F>, the  standard  presentation
for the  $p$-quotient  of  <F>  specified  by  the  second  argument  and
<options>, as a pc group. Behind the colon, <options> is a  selection  of
the options from the following list should be given, separated by  commas
like  record   components   (see~"ref:function   call   with   options").
Section~"Hints and Warnings regarding the  use  of  Options"  gives  some
important hints and warnings regarding option usage; if you haven't  read
this section, please take the time to read it now.

If  the  user  supplies   a   prime   <p>   as   second   argument   then
`PqStandardPresentation'    or    `StandardPresentation'    computes    a
$p$-quotient of <F> for the prime <p>.

Alternatively, a user may supply a pc group <G> as second argument  which
is a $p$-quotient of <F>, and if she does so, the automorphism  group  of
<G> must be known. The presentation for <G>  can  be  constructed  by  an
initial call to `Pq' (see "Pq"). For <G> one usually chooses  a  class  1
$p$-quotient of <F>, since the automorphism group of <G>  must  be  known
and this is most readily available when  <G>  is  an  elementary  abelian
group. Where the necessary information is available for a $p$-quotient of
higher class, one can apply the standard presentation algorithm from that
class onwards.

The following options are supported.

\beginitems
`ClassBound := <n>' &
Specifies  the  standard  presentation  is  computed  for   the   largest
$p$-quotient of <F> having lower expon\-ent-$p$ class at most <n>.

`Exponent := <n>' &
Specifies the $p$-quotient computed has  exponent  <n>.  By  default,  no
exponent law is enforced.

`Metabelian' &
Specifies the $p$-quotient constructed is  metabelian.  By  default  this
restriction is not enforced.

`PcgsAutomorphisms' &
Specifies that a polycyclic  generating  sequence  for  the  automorphism
group of <G> (which must be *soluble*), be computed  and  passed  to  the
`pq' binary. This increases the efficiency of the  computation;  it  also
prevents the `pq' from calling {\GAP} for orbit-stabilizer  calculations.
See  section  "Computing  Descendants   of   a   p-Group"   for   further
explanations.

&
*Note:*
If `PcgsAutomorphisms' is used when the  automorphism  group  of  <G>  is
insoluble, an error message occurs.

`OutputLevel := <n>' &
Specifies the level of ``verbosity'' of the information output by the ANU
`pq' binary when computing a pc presentation; <n> must be an  integer  in
the range 0 to 3. `OutputLevel := 0' displays no output, `OutputLevel  :=
1' displays minimal output (which  is  the  `pq'  binary's  default)  and
`OutputLevel's for <n> greater than `1' give progressively  more  output.
To see these messages from the `pq' binary, the `InfoANUPQ' level must be
set to 2 (see~"InfoANUPQ").

`SetupFile := <filename>' &
Non-interactively, this option directs that `pq' should not be called and
that an input file  with  name  <filename>  (a  string),  containing  the
commands necessary for the  ANU  `pq'  standalone,  be  constructed.  The
commands written to <filename> are also `Info'-ed behind  a  ```ToPQ> '''
prompt at `InfoANUPQ' level  3  (see~"InfoANUPQ").  In  this  case,  `Pq'
returns `true'. Interactively, `SetupFile' is ignored.

`PqWorkspace := <workspace>' &
Non-interactively, this option sets the memory used by the  `pq'  binary.
It sets the maximum number of integer-sized elements to allocate  in  its
main storage array. By default, the  `pq'  binary  sets  this  figure  to
10000000. Interactively, `PqWorkspace' is ignored;  the  memory  used  in
this  case  may  be  set  by   giving   `PqStart'   a   second   argument
(see~"PqStart").

\enditems

The options for `PqStandardPresentation' may also be passed  in  the  two
alternative ways described for  `Pq'  (see~"Pq").  `StandardPresentation'
does not provide these alternative ways of passing options. Options  that
are not passed are set to their default values.

We illustrate the method with the following examples.

\beginexample
gap> F := FreeGroup( "a", "b" );; a := F.1;; b := F.2;;
gap> G := F / [a^25, Comm(Comm(b, a), a), b^5];        
<fp group on the generators [ a, b ]>
gap> StandardPresentation( G, 5 : ClassBound := 10 );  
<fp group on the generators [ f1, f2, f3, f4, f5, f6, f7, f8, f9, f10, f11, 
  f12, f13, f14, f15, f16, f17, f18, f19, f20, f21, f22, f23, f24, f25, f26 ]>
gap> H := F / [ a^625, Comm(Comm(Comm(Comm(b, a), a), a), a)/Comm(b, a)^5,
>               Comm(Comm(b, a), b), b^625 ];;                     
gap> StandardPresentation( H, 5 : ClassBound := 15, Metabelian );
<fp group on the generators [ f1, f2, f3, f4, f5, f6, f7, f8, f9, f10, f11, 
  f12, f13, f14, f15, f16, f17, f18, f19, f20 ]>
gap> F4 := FreeGroup( "a", "b", "c", "d" );;                        
gap> a := F4.1;; b := F4.2;; c := F4.3;; d := F4.4;;
gap> G4 := F4 / [ b^4, b^2 / Comm(Comm (b, a), a), d^16,                
>                 a^16 / (c * d), b^8 / (d * c^4) ];
<fp group on the generators [ a, b, c, d ]>
gap> K := Pq( G4 : Prime := 2, ClassBound := 1 );
<pc group of size 4 with 2 generators>
gap> AutomorphismGroup( K );
<group with 4 generators>
gap> IsSolvable( last );
true
gap> StandardPresentation( G4, K : ClassBound := 14, PcgsAutomorphisms );
<fp group with 53 generators>
\endexample

(These functions require the {\ANUPQ}  share  package;  see~"Loading  the
ANUPQ Share Package".)

\>EpimorphismPqStandardPresentation( <F>, <p> : <options> ) F
\>EpimorphismPqStandardPresentation( <F>, <G> : <options> ) F
\>EpimorphismStandardPresentation( <F>, <p> : <options> ) M
\>EpimorphismStandardPresentation( <F>, <G> : <options> ) M

Each of the above functions accepts the same arguments and options as the
function `StandardPresentation' (see~"StandardPresentation") and  returns
an epimorphism from the finitely presented group <F>  onto  the  finitely
presented group given by a standard  presentation,  i.e.~if  <S>  is  the
standard presentation computed for  <F>  by  `StandardPresentation'  then
`EpimorphismStandardPresentation' returns the epimorphism from <F> to the
group with presentation <S>.

We illustrate the function with the following example.

\beginexample
gap> F := FreeGroup (6);;
gap> x := F.1;; y := F.2;; z := F.3;; w := F.4;; a := F.5;; b := F.6;;
gap> R := [x^3 / w, y^3 / w * a^2 * b^2, w^3 / b,
>          Comm (y, x) / z, Comm (z, x), Comm (z, y) / a, z^3 ];;
gap> Q := F / R;;
gap> G := Pq ( Q : Prime := 3, ClassBound := 3 );
<pc group of size 729 with 6 generators>
gap> phi := EpimorphismStandardPresentation( Q, 3 : ClassBound := 3 );
[ f1, f2, f3, f4, f5, f6 ] -> [ f1*f2^2*f3*f4^2*f5^2, f1*f2*f3*f5, f3^2, 
  f4*f6^2, f5, f6 ]
gap> Size( Image(phi) );
729
\endexample

(These functions require the {\ANUPQ}  share  package;  see~"Loading  the
ANUPQ Share Package".)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Testing p-Groups upon Isomorphism}

\>IsPqIsomorphicPGroup( <G>, <H> ) F
\>IsIsomorphicPGroup( <G>, <H> ) M

each return true if <G> is isomorphic to <H>, where both <G> and <H> must
be pc groups of prime power order. These functions  compute  and  compare
the standard presentations for <G> and <H> (see "StandardPresentation").


\beginexample
gap> G := Group( (1,2,3,4), (1,3) );
Group([ (1,2,3,4), (1,3) ])
gap> P1 := Image( IsomorphismPcGroup( G ) );
Group([ f1, f2, f3 ])
gap> P2 := SmallGroup( 8, 5 );
<pc group of size 8 with 3 generators>
gap> IsIsomorphicPGroup( P1, P2 );
false
gap> P3 := SmallGroup( 8, 4 );
<pc group of size 8 with 3 generators>
gap> IsIsomorphicPGroup( P1, P3 );
false
gap> P4 := SmallGroup( 8, 3 );
<pc group of size 8 with 3 generators>
gap> IsIsomorphicPGroup( P1, P4 );
true
\endexample

(These functions require the {\ANUPQ}  share  package;  see~"Loading  the
ANUPQ Share Package".)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing Descendants of a p-Group}

\>PqDescendants( <G> : <options> ) F

returns, for the pc-group <G> which must be of prime power order  with  a
confluent pc-presentation (see~"ref:IsConfluent!for pc groups"),  a  list
of descendants (pc groups) of <G>.

If <G> does *not* have $p$-class 1, then the automorphism  group  of  <G>
must be known. In practice, the automorphism group  of  <G>  is  computed
using the {\GAP} command `AutomorphismGroup'. Note that the {\GAP}  share
package {\AutPGrp} provides an algorithm for computing  the  automorphism
group of a $p$-group which performs better than the  standard  method  in
{\GAP}.

If the automorphism group  of  <G>  is  not  soluble  or  if  the  option
`PcgsAutomorphisms' (see below) is not used, then the  `pq'  binary  will
call  {\GAP}  together  with   the   {\AutPGrp}   package   for   certain
orbit-stabilizer calculations.

The  following  options  are  supported.  Section~"Hints   and   Warnings
regarding the use of Options" gives some  important  hints  and  warnings
regarding option usage; if you haven't read this section, please take the
time to read it now.

\beginitems

`ClassBound := <n>' &
Specifies that only descendants with lower exponent-$p$ class at most <n>
(an integer) be generated. The default value is the exponent-$p$ class of
<G> plus one.

`OrderBound := <n>' &
Specifies that only descendants of size at most $p^<n>$, where <n>  is  a
non-negative integer,  be  generated.  Note  that  you  cannot  set  both
`OrderBound' and `StepSize'.

`StepSize := <n>' &
Specifies that only  those  immediate  descendants  which  are  a  factor
$p^<n>$ bigger than their parent group, where <n> is a positive  integer,
be generated.

`StepSize := <list>' &
If <list> is a list of positive integers such that the sum of the  length
of <list> and the exponent-$p$ class of <G> is equal to the  class  bound
defined by the option `ClassBound', then the integers of <list>  are  the
step sizes for each additional class.

`PcgsAutomorphisms' &
Specifies that a polycyclic  generating  sequence  for  the  automorphism
group of <G> (which must be *soluble*), be computed  and  passed  to  the
`pq' binary. This increases the efficiency of the  computation;  it  also
prevents the `pq' from calling {\GAP} for orbit-stabilizer  calculations.
See  section  "Computing  Descendants   of   a   p-Group"   for   further
explanations.

&
*Note:*
If `PcgsAutomorphisms' is used when the  automorphism  group  of  <G>  is
insoluble, an error message occurs.

`RankInitialSegmentSubgroups := <n>' &
Sets the rank of the initial  segment  subgroup  chosen  to  be  <n>.  By
default, this has value 0.

`SpaceEfficient' &
Specifies that the `pq' binary perform calculations more slowly but  with
greater space efficiency. This flag is frequently necessary for groups of
large Frattini quotient rank. The space saving occurs  because  only  one
permutation is stored at any one time. This option is only  available  in
conjunction with the `PcgsAutomorphisms' flag.

`AllDescendants' &
By default, only *capable* descendants are constructed. If this  flag  is
set, all descendants are computed.

`Exponent := <n>' &
Specifies that only descendants with  exponent  <n>  be  constructed.  By
default there is no exponent law.

`Metabelian' &
Specifies that only metabelian descendants  be  constructed.  By  default
this restriction is not enforced.

`SubList := <sub>' &
Suppose that <L> is the list of descendants generated, then  for  a  list
<sub> of integers this option causes `PqDescendants' to return  `Sublist(
<L>, <sub> )'. If an integer <n>  is  supplied,  `PqDescendants'  returns
`<L>[<n>]'.

`SetupFile := <filename>' &
Non-interactively, this option directs that `pq' should not be called and
that an input file  with  name  <filename>  (a  string),  containing  the
commands necessary for the  ANU  `pq'  standalone,  be  constructed.  The
commands written to <filename> are also `Info'-ed behind  a  ```ToPQ> '''
prompt at `InfoANUPQ' level  3  (see~"InfoANUPQ").  In  this  case,  `Pq'
returns `true'. Interactively, `SetupFile' is ignored.

`PqWorkspace := <workspace>' &
Non-interactively, this option sets the memory used by the  `pq'  binary.
It sets the maximum number of integer-sized elements to allocate  in  its
main storage array. By default, the  `pq'  binary  sets  this  figure  to
10000000. Interactively, `PqWorkspace' is ignored;  the  memory  used  in
this  case  may  be  set  by   giving   `PqStart'   a   second   argument
(see~"PqStart").

\enditems

Alternatively,  you can pass `PqDescendants'  a record  as  a  parameter,
which  contains  as  entries some (or all) of the above mentioned.  Those
parameters  which do not occur  in  the record are  set  to their default
values.

Note that you cannot set both `OrderBound' and `StepSize'.

In the first example  we  compute all descendants of the Klein four group
which have exponent-2 class at most 5 and order at most $2^6$.

\beginexample
gap> F := FreeGroup( "a", "b" );; a := F.1;; b := F.2;;         
gap> G := PcGroupFpGroup( F / [ a^2, b^2, Comm(b, a) ] );
<pc group of size 4 with 2 generators>
gap> des := PqDescendants( G : OrderBound := 6, ClassBound := 5,
>                              AllDescendants );;
gap> Length(des);
83
gap> List(des, Size); 
[ 8, 8, 8, 16, 16, 16, 32, 16, 16, 16, 16, 16, 32, 32, 64, 64, 32, 32, 32, 
  32, 32, 32, 32, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 32, 32, 32, 32, 
  64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 32, 32, 32, 32, 32, 64, 64, 64, 
  64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 64, 
  64, 64, 64, 64, 64, 64, 64 ]
gap> List(des, d -> Length( PCentralSeries( d, 2 ) ) - 1 );
[ 2, 2, 2, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 
  3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 3, 4, 
  4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 4, 
  4, 4, 4, 5, 5, 5, 5, 5 ]
\endexample

In the second example we compute all  capable descendants of order  27 of
the  elementary abelian group of order 9.  

\beginexample
gap> F := FreeGroup( 2, "g" );;                                  
gap> G := PcGroupFpGroup( F / [ F.1^3, F.2^3, Comm(F.1, F.2) ] );
<pc group of size 9 with 2 generators>
gap> A := AutomorphismGroup( G );
<group with 4 generators>
gap> IsSolvable(A);
true
gap> Pcgs(A);
Pcgs([ [ g1, g2 ] -> [ g1, g2^2 ], [ g1, g2 ] -> [ g1, g1*g2 ], 
  [ g1, g2 ] -> [ g1^2*g2, g1*g2 ], [ g1, g2 ] -> [ g2^2, g1 ], 
  [ g1, g2 ] -> [ g1^2, g2^2 ] ])
gap> des := PqDescendants( G : OrderBound := 3, ClassBound := 2,
>                              PcgsAutomorphisms );
[ <pc group of size 27 with 3 generators>, 
  <pc group of size 27 with 3 generators> ]
gap> List(des, d -> Length( PCentralSeries( d, 3 ) ) - 1 );
[ 2, 2 ]
\endexample

In  the  third  example,  we  compute  all  capable  descendants  of  the
elementary abelian group of order  $5^2$ which have exponent-$5$ class at
most $3$, exponent $5$, and are metabelian.

\beginexample
gap> F := FreeGroup( 2, "g" );;                                  
gap> G := PcGroupFpGroup( F / [ F.1^5, F.2^5, Comm(F.2, F.1) ] );
<pc group of size 25 with 2 generators>
gap> des := PqDescendants( G : Metabelian, ClassBound := 3,
>                              Exponent := 5 );
[ <pc group of size 125 with 3 generators>, 
  <pc group of size 625 with 4 generators>, 
  <pc group of size 3125 with 5 generators> ]
gap> List(des, d -> Length( PCentralSeries( d, 5 ) ) - 1 );
[ 2, 3, 3 ]
gap> List(des, d -> Length( DerivedSeries( d ) ) );
[ 3, 3, 3 ]
gap> List(des, d -> Maximum( List( Elements(d), Order ) ) );     
[ 5, 5, 5 ]
\endexample

(The function requires the {\ANUPQ} share package; see~"Loading the ANUPQ
Share Package".)

\>PqList( <filename> [: SubList := <sub> ]) F

reads a file with name <filename> (a string) and returns the list <L>  of
pc groups (or with option `SubList' a sublist of <L> or a single pc group
in <L>) defined in that file. If the option `SubList' is passed  and  has
the value <sub>, then it has the same  meaning  as  for  `PqDescendants',
i.e.~if  <sub>  is  an  integer  then  `PqList'   returns   `<L>[<sub>]';
otherwise, if <sub> is a list of integers `PqList' returns  `Sublist(<L>,
<sub> )'.

Both `PqList' and `SavePqList' (see "SavePqList") can be used to save and
restore a list of descendants (see "PqDescendants").

(The function requires the {\ANUPQ} share package; see~"Loading the ANUPQ
Share Package".)

\>SavePqList( <filename>, <list> ) F

writes a list of descendants <list> to a file  with  name  <filename>  (a
string).

`SavePqList' and `PqList' (see "PqList") can be used to save and restore,
respectively, the results of `PqDescendants' (see "PqDescendants").

(The function requires the {\ANUPQ} share package; see~"Loading the ANUPQ
Share Package".)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%E
